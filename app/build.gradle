apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "org.docspell.docspellshare"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 6
        // keep this format or change github workflows bash quirks
        versionName "0.6.0-SNAPSHOT"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            // Optional, specify signing versions used
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

if (project.hasProperty('KEYSTORE_FILE') &&
    project.hasProperty('KEYSTORE_PASSWORD') &&
    project.hasProperty('KEY_ALIAS') &&
    project.hasProperty('KEY_PASSWORD')) {
    android.signingConfigs.release.storeFile = file(KEYSTORE_FILE)
    android.signingConfigs.release.storePassword = KEYSTORE_PASSWORD
    android.signingConfigs.release.keyAlias = KEY_ALIAS
    android.signingConfigs.release.keyPassword = KEY_PASSWORD
} else {
    android.buildTypes.release.signingConfig = null
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}
